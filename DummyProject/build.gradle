buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

minecraft {
	mappings channel: 'official', version: minecraft_version

	runs {
		client {
			workingDirectory project.file('run')
			ideaModule "${rootProject.name}.${project.name}.main"
			taskName 'Client'
			jvmArgs "-Dforge.logging.console.level=debug", "-Dforge.logging.markers=SCAN"
			mods {
				modClientRun {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			ideaModule "${rootProject.name}.${project.name}.main"
			taskName 'Server'
			jvmArgs "-Dforge.logging.console.level=debug", "-Dforge.logging.markers=SCAN"
			mods {
				modServerRun {
					source sourceSets.main
				}
			}
		}
	}
}

// TODO for convenience during testing we probably want a mods folder to load fabric mods from - where should this be?
//repositories {
//	flatDir {
//		dir "libs"
//	}
//}

configurations {
	library
	implementation.extendsFrom library
}

minecraft.runs.all {
	lazyToken("minecraft_classpath") {
		configurations.library.copyRecursive().resolve().collect {it.absolutePath}.join(File.pathSeparator)
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	library (project(path: ":ModLocator", configuration: "mainJar")) { transitive = false }
}
